@echo on

:PING_TEST
SET SERVERIP=192.168.4.2
PING %SERVERIP% -n 1 | FIND /I "TTL="
IF NOT %ERRORLEVEL%==0 GOTO PING_TEST

:SET_TIME
NET TIME \\%SERVERIP% /SET /Y

:GETCTCODEFILE_FTP
IF EXIST %CTCODE%.TXT GOTO SET_HWINFO
IF %CTCODE:~0,1%==M SET FTPPATH=MAIN
IF %CTCODE:~0,1%==S SET FTPPATH=SUB
CALL fileget.bat %SERVERIP% ftp ftp SYSINFO/%FTPPATH%/%FBTPRODUCT% %CTCODE%.TXT
IF NOT %ERRORLEVEL%==0 (
	SET RESULT_SET=Main CTCode : %CTCODE%
	SET RESULT_GET=Get CTCode Fail
	GOTO FAIL
)

:SET_HWINFO
COPY %CTCODE%.TXT HWINFO.CMD /Y
IF NOT %ERRORLEVEL%==0 (
	SET RESULT_SET=Create hwInfo Fail
	SET RESULT_GET=Please Re-Try
	GOTO FAIL
)

CALL HWINFO.CMD

REM =======================================================================
REM PCB System info
REM =======================================================================

IF %CTCODE:~0,1%==S GOTO CHECK_FINGERPRT_DEVICE

:CHECK_CPU_INFO
FOR /F "skip=1 tokens=*" %%a IN ('WMIC CPU GET Name ^| Findstr "."') DO (SET RESULT_GET=CPU : %%a)
ECHO %RESULT_GET% > CPUINFO.TXT
FIND /I "%CPU%" CPUINFO.TXT
IF NOT %ERRORLEVEL%==0 (
	SET RESULT_SET=CPU : %CPU%
        	GOTO FAIL
)

:CHECK_MEMORY_SIZE
SET GETMEMSIZE_TOTAL=0
SET GETMEMCOUNT=0
FOR /F "skip=1" %%a IN ('WMIC MEMORYCHIP GET Capacity ^| findstr "."') DO (CALL :SETTOTALMEMSIZE "%%a")

IF NOT *%MEMTOTAL%==*%GETMEMSIZE_TOTAL%GB (
	SET RESULT_SET=MEM : %MEMTOTAL%
	SET RESULT_GET=MEM : %GETMEMSIZE_TOTAL%GB
	GOTO FAIL
)

IF *%MEMCOUNT%==*ON-BOARD GOTO CHECK_VGA

IF NOT *%MEMCOUNT%==*%GETMEMCOUNT% (
	SET RESULT_SET=MEMCOUNT : %MEMCOUNT%
	SET RESULT_GET=MEMCOUNT : %GETMEMCOUNT%
	GOTO FAIL
)
GOTO CHECK_VGA

:SETTOTALMEMSIZE
SET SUBMEM=%~1
SET /A SUBMEM=%SUBMEM:~0,-3%/1024/1024
SET /A GETMEMSIZE_TOTAL+=%SUBMEM%
SET /A GETMEMCOUNT+=1
GOTO :EOF

:CHECK_VGA
SET RESULT_GET=
SET GETVGACOUNT=0
FOR /F "tokens=* skip=1" %%a IN ('WMIC PATH Win32_VideoController Get Caption ^| Find /I " "') DO (CALL :ADDVGAINFO "%%a")

IF NOT *%VGACOUNT%==*%GETVGACOUNT% (
	SET RESULT_SET=SET VGACOUNT : %VGACOUNT%
	SET RESULT_GET=GET VGACOUNT : %GETVGACOUNT%
	GOTO FAIL
)
GOTO CHECK_DISK_COUNT

:ADDVGAINFO
SET RESULT_GET=%~1 %RESULT_GET%
SET /A GETVGACOUNT+=1
GOTO :EOF

:CHECK_DISK_COUNT
SET GETDISKCOUNT=0
FOR /F %%a in ('WMIC DISKDRIVE GET MediaType ^| Find /i /c "Fixed hard disk media"') do (set GETDISKCOUNT=%%a)
IF NOT *%DISKCOUNT%==*%GETDISKCOUNT% (	
	SET RESULT_SET=DISKCOUNT : %DISKCOUNT%
	SET RESULT_GET=DISKCOUNT : %GETDISKCOUNT%
	GOTO FAIL	
)

:CHECK_THUNDERBOLT_DEVICE
SET GETTHUNDERBOLT=N
for /f "tokens=*" %%a in ('GetThunderBoltSupport.exe') do (set "THUNDERBOLT_STATUS=%%a")
IF "%THUNDERBOLT_STATUS%"=="Support" SET GETTHUNDERBOLT=Y
IF NOT *%THUNDERBOLT%==*%GETTHUNDERBOLT% (
	SET RESULT_SET=THUNDERBOLT : %THUNDERBOLT%
	SET RESULT_GET=THUNDERBOLT : %GETTHUNDERBOLT%
	GOTO FAIL
)

:CHECK_BACKLIT_DEVICE
SET GETBACKLIT=N
for /f "tokens=*" %%a in ('BacklitTest.exe /status') do (set "BACKLIT_STATUS=%%a")
IF "%BACKLIT_STATUS%"=="Supported" SET GETBACKLIT=Y
IF NOT *%BACKLIT%==*%GETBACKLIT% (
	SET RESULT_SET=BACKLIT : %BACKLIT%
	SET RESULT_GET=BACKLIT : %GETBACKLIT%
	GOTO FAIL
)

:CHECK_TOUCH_DEVICE
SET GETTOUCH=N
for /f "skip=1" %%a in ('WMIC PATH WIN32_PNPENTITY WHERE "Caption LIKE '%%touch screen%%'" GET Status ^| Find /I " "') do (set TOUCH_STATUS=%%a)
IF *%TOUCH_STATUS%==*OK SET GETTOUCH=Y
IF NOT *%TOUCH%==*%GETTOUCH% (
	SET RESULT_SET=TOUCH : %TOUCH%
	SET RESULT_GET=TOUCH : %GETTOUCH%
	GOTO FAIL
)

:CHECK_IRCAMERA_DEVICE
SET GETIRCAMERA=N
for /f "skip=1" %%a in ('WMIC PATH WIN32_PNPENTITY WHERE "Caption LIKE '%%Facial Recognition%%'" GET Status ^| Find /I " "') do (set IRCAMERA_STATUS=%%a)
IF *%IRCAMERA_STATUS%==*OK SET GETIRCAMERA=Y
IF NOT *%IRCAMERA%==*%GETIRCAMERA% (
	SET RESULT_SET=IRCAMERA : %IRCAMERA%
	SET RESULT_GET=IRCAMERA : %GETIRCAMERA%
	GOTO FAIL
)

:CHECK_FINGERPRT_DEVICE
SET GETFINGERPRT=N
For /F "skip=1" %%a in ('WMIC PATH WIN32_PNPENTITY WHERE "Caption LIKE '%%Fingerprint%%'" Get Status ^| FIND /I " "') Do (set FINGERPRT_STATUS=%%a)
IF *%FINGERPRT_STATUS%==*OK SET GETFINGERPRT=Y
IF NOT *%FINGERPRT%==*%GETFINGERPRT% (
	SET RESULT_SET=FINGERPRT : %FINGERPRT%
	SET RESULT_GET=FINGERPRT : %GETFINGERPRT%
	GOTO FAIL
)

:CHECK_FUNCTIONTEST_ITEM
IF *%FANCOUNT%==*  (
	SET RESULT_SET=FANCOUNT NOT SET
	SET RESULT_GET=FANCOUNT NOT SET
	GOTO FAIL
)

IF *%IRCAMERA%==*  (
	SET RESULT_SET=IRCAMERA NOT SET
	SET RESULT_GET=IRCAMERA NOT SET
	GOTO FAIL
)

IF *%FUNCTIONTESTINI%==*  (
	SET RESULT_SET=FUNCTIONTESTINI NOT SET
	SET RESULT_GET=FUNCTIONTESTINI NOT SET
	GOTO FAIL
)

IF *%PORTTESTINI%==*  (
	SET RESULT_SET=PORTTESTINI NOT SET
	SET RESULT_GET=PORTTESTINI NOT SET
	GOTO FAIL
)

:PASS
TASKKILL /F /IM SysResultView.exe
EXIT /B 0
GOTO END

:FAIL
DEL %CTCODE%.TXT
TASKKILL /F /IM SysResultView.exe
START SysResultView.exe NG "%RESULT_SET%" "%RESULT_GET%"
PowerShell Start-sleep -second 20
GOTO SETCTCODE

:END